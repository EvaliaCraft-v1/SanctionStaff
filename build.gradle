import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java-library'
    id 'io.github.goooler.shadow' version '8.1.7'
}
repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = 'spigot'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
    	url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
    	url = "https://nexus.scarsz.me/content/groups/public/"
    	name = "discordsrv"
    }
    maven {
        name = 'paper'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
    	name = "Eliapp"
    	url = "https://repo.eliapp.fr:8081/repository/snapshots/"
    }
    maven {
        name "essentialsx-releases"
        url "https://repo.essentialsx.net/releases/"
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.2-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot:1.20.2-R0.1-SNAPSHOT'
    compileOnly 'io.github.waterfallmc:waterfall-api:1.20-R0.1-SNAPSHOT'
    compileOnly 'com.discordsrv:discordsrv:1.27.0'
    compileOnly 'net.essentialsx:EssentialsX:2.20.1'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

archivesBaseName = 'SanctionStaff'

sourceSets.main {
    java.srcDirs = ['src']
    resources.srcDirs = ['resources']
}

processResources {
    def props = [version: version]
    inputs.properties(props)
    // Do not try to expand service files, they may contain a dollar sign to mark inner classes
    filesMatching('plugin.yml') {
        expand(props)
    }
}

compileJava.options.encoding = 'UTF-8'

task prepareServer(type: Copy) {
    from shadowJar.outputs
    def pluginDir = System.getProperty('plugins.dir')
    if (pluginDir == null) {
        enabled(false)
    } else {
        into pluginDir
    }
}

build.dependsOn shadowJar

shadowJar {
    archiveFileName = "SanctionStaff.jar"
}
